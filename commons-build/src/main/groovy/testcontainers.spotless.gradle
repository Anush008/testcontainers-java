import com.diffplug.gradle.spotless.SpotlessApply
import com.diffplug.gradle.spotless.SpotlessCheck

plugins {
    id 'java'
    id 'com.diffplug.spotless'
}

spotless {
    java {
        toggleOffOn()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()

        prettier(['prettier': '2.5.1', 'prettier-plugin-java': '1.6.1'])
            .config([
                'parser'    : 'java',
                'tabWidth'  : 4,
                'printWidth': 120
            ])

        importOrder('', 'java', 'javax', '\\#')
    }
    groovyGradle {
        target '**/*.groovy'
        greclipse('4.26')
    }
}

tasks.withType(SpotlessApply).configureEach {
    notCompatibleWithConfigurationCache("https://github.com/diffplug/spotless/issues/644")
}

tasks.withType(SpotlessCheck).configureEach {
    notCompatibleWithConfigurationCache("https://github.com/diffplug/spotless/issues/644")
}
