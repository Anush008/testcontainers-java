plugins {
    id 'java'
    id 'checkstyle'
    id 'com.diffplug.spotless'
}

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = "10.12.4"
    def rootProjectPath = rootProject.name == 'testcontainers-java' ? "" : "../"
    configFile = rootProject.layout.projectDirectory.file(rootProjectPath + "config/checkstyle/checkstyle.xml").asFile
}

spotless {
    java {
        toggleOffOn()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()

        prettier(['prettier': '2.5.1', 'prettier-plugin-java': '1.6.1'])
            .config([
                'parser'    : 'java',
                'tabWidth'  : 4,
                'printWidth': 120
            ])

        importOrder('', 'java', 'javax', '\\#')
    }
    groovyGradle {
        target '**/*.groovy'
        greclipse('4.19.0')
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
